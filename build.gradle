apply plugin: 'scala'
apply plugin: 'java'

import groovy.io.FileType
import org.apache.tools.ant.filters.ReplaceTokens

defaultTasks 'build'
//defaultTasks 'compileScala'

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url "http://repo.bukkit.org/content/repositories/snapshots/"
  }
  maven {
    url "http://maven.clapper.org/"
  }
  maven {
    url "http://dl.bintray.com/joshcough/maven/"
  }
}

// See http://goo.gl/sN5GVB
configurations {
  sshAntTask
}

ext.author       = "Brian M. Clapper"
ext.pluginName   = "RandomTeleport"
ext.pluginClass  = "org.clapper.minecraft.randomteleport.RandomTeleportPlugin"
version          = "0.4"
ext.pluginYaml   = "src/main/resources/plugin.yml"

ext.mclibVersion = "0.6"

dependencies {
  runtime "org.scala-lang:scala-library:2.10.2"
  compile "org.scala-lang:scala-compiler:2.10.2"
  // When calling anything directly on the Bukkit object, the Servlet API
  // is (often) required to avoid weird compilation errors related to a
  // stub version included in the Bukket jar.
  compile "javax.servlet:servlet-api:2.5"
  compile "org.bukkit:bukkit:1.6.4-R0.1-SNAPSHOT"
  compile "org.bukkit:craftbukkit:1.6.4-R0.1-SNAPSHOT"
  compile "org.clapper.minecraft:mclib:${ext.mclibVersion}"
  compile "com.joshcough:scala-minecraft-plugin-api_2.10:0.3.2"

  runtime("com.joshcough:scala-minecraft-plugin-api_2.10:0.3.2") {
    transitive = false
  }
  runtime("com.joshcough:scala-minecraft-scala-library_2.10:0.3.2") {
    transitive = false
  }
  runtime("org.clapper.minecraft:mclib:${ext.mclibVersion}") {
    transitive = false
  }

  sshAntTask "org.apache.ant:ant-jsch:1.9.2", "com.jcraft:jsch:0.1.50"
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
  scalaCompileOptions.additionalParameters = ["-feature"]

  configure(scalaCompileOptions.forkOptions) {
    memoryMaximumSize = '1g'
  }
}

clean.dependsOn('localClean')

task localClean(type: Delete) {
  delete project.ext.pluginYaml
}

task zip(dependsOn: ['zipCopy', 'zipJars'])

task zipCopy(type: Copy, dependsOn: 'jar') {
  from fileTree('build/libs') {
    include "*.jar"
  }
  from configurations.runtime.files
  into 'build/zipcopy'
}

task zipCleanup(type: Delete) {
  delete 'build/zipcopy'
}

task zipJars(type: Zip) {
  archiveName "mc-random-teleport-${version}.zip"
  includeEmptyDirs false
  finalizedBy 'zipCleanup'

  destinationDir file("build/distributions")

  from fileTree('build/zipcopy') {
    include "mclib*.jar"
    include "scala-library*.jar"
    include "RandomTeleport*.jar"
    include "scala-minecraft-plugin-api*.jar"
  }

  into "/mc-random-teleport"
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}

task createPluginYaml(type: Copy) {
  from("templates") {
    include "plugin.yml"
  }
  into 'src/main/resources'
  filter(ReplaceTokens, tokens: [
    version: version,
    name:    project.ext.pluginName,
    class:   project.ext.pluginClass,
    genTime: new Date().format("yyyy/MM/dd HH:mm:ss"),
    author:  project.ext.author
  ])
}

processResources.dependsOn('createPluginYaml')


// Publish the zip file to the remote server.
task publish(dependsOn: 'zip') {

  // Get the passphrase from the user.
  def console = System.console()
  def passphrase = console.readPassword("\n%s: ", "SSH keyfile passphrase")

  // Redefine the scp Ant task, with our classpath.
  ant.taskdef(name:      'scp',
    classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
    classpath: configurations.sshAntTask.asPath)

  // Invoke the scp Ant task. (Use "gradle -i publish" to see SSH output.)
  // Assumes PUBLISH_TO and SSH_KEYFILE are set in the environment (e.g.,
  // via a direnv ".envrc" file).
  ant.scp(todir:      System.env["PUBLISH_TO"] + "/" + "mc-random-teleport",
    keyfile:    System.env["SSH_KEYFILE"],
    passphrase: passphrase as String,
    verbose:    'true') {
    fileset(dir: 'build/distributions') {
      include(name: '*.zip')
    }
  }
}

jar {
  manifest {
    def manifestCP = ['../lib/mclib.jar',
                      '../lib/scala-library-2.10.2.jar',
                      'scala-minecraft-scala-library-2.10-0.3.2.jar',
                      'scala-minecraft-plugin-api_2.10-0.3.2.jar',
                      '../craftbukkit.jar']
    attributes 'Class-Path': manifestCP.join(" ")
    baseName = "RandomTeleport"
  }
}

// See http://sethrylan.org/2013/07/02/scala-gradle-scalaconsole.html

scalaConsole.dependsOn(build)
scalaConsole.classpath += sourceSets.main.output
task console(dependsOn: "scalaConsole")

